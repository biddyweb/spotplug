import com.plugtree.spotplug.model.GenericEvent;
import com.plugtree.spotplug.impl.User;
import com.plugtree.spotplug.actuator.MailActuator;
import com.plugtree.spotplug.impl.UsersList;
import com.plugtree.spotplug.Actuator;

import java.lang.Number;
import java.lang.String;
import java.lang.Long;
import java.lang.Integer;
import java.util.concurrent.ConcurrentHashMap;
import org.drools.runtime.rule.RuleContext;
import java.util.*;

global ConcurrentHashMap hashMap;
global Actuator actuator;
global MailActuator mailActuator;

declare GenericEvent
    @role(event)
    @timestamp(callDateTime.getTime())
    @duration(callDuration)    
end

declare BankEvent
    @role(event)
    @timestamp(callDateTime.getTime())
    @duration(callDuration)      
    userId : String
    sequentialID : long
    transactionID : long
    opCode : long
    amount : int
	callDateTime : Date
	callDuration : long
end

# Converting Generic Events into a BankEvent
rule "Convert GenericEvent into a bank-event"
dialect "mvel"
no-loop
salience 10000

when
	$ge : GenericEvent(eventType == "bank-event") from entry-point GenericEventEntryPoint
then
    BankEvent bankEvent = new BankEvent();
    bankEvent.setAmount(Integer.parseInt($ge.getAttributes().get("amount")));
    bankEvent.setSequentialID(Long.parseLong($ge.getAttributes().get("sequentialID")));
    bankEvent.setTransactionID(Long.parseLong($ge.getAttributes().get("transactionID")));
    bankEvent.setOpCode(Long.parseLong($ge.getAttributes().get("opCode")));

    bankEvent.setCallDateTime($ge.getCallDateTime());
    bankEvent.setCallDuration($ge.getCallDuration());

	bankEvent.setUserId($ge.getAttributes().get("userId"));
	retract($ge);
	entryPoints["BankEvent"].insert(bankEvent);
end

/*
# 1st Fraud Pattern
rule "Strange volume in transaction"
dialect "mvel" 

when
	 $user : User()
	 $n : Number($totalAmount : intValue) from accumulate($event : BankEvent(userId == $user.id) over window:time(1d), sum($event.amount))
then
	System.out.println("Activada!" + $totalAmount);
	//hashMap.get($user.id).increaseFraudProbability(30);
	actuator.writeOutput(kcontext, hashMap.get($user.id));
	System.out.println("HOla");	
end
*/

# 2nd Fraud Pattern
rule "Transaction at unusual hours"
dialect "mvel"

when
	    $event : BankEvent( callDateTime.hours >= 2,
	                        callDateTime.hours <= 6,
	                        amount > 4000) from entry-point BankEvent
then

    	 actuator.writeOutput(kcontext, hashMap.get($event.userId));
end


# 3th Fraud Pattern
rule "Same BankEvent, same time, everyday"
dialect "mvel" 

when
	$event : BankEvent() from entry-point BankEvent
	BankEvent(callDateTime.day > $event.callDateTime.day,
				 callDateTime.seconds == $event.callDateTime.seconds,
				 callDateTime.minutes == $event.callDateTime.minutes,
				 callDateTime.hours == $event.callDateTime.hours,
				 userId == $event.userId,
				 amount == $event.amount) from entry-point BankEvent 
then
	hashMap.get($event.userId).increaseFraudProbability(60);
	actuator.writeOutput(kcontext, hashMap.get($event.userId));
end



# 4th Fraud Pattern
rule "Incorrect Message Order"
dialect "mvel"

when
	$user : User()
	$e : BankEvent($userId : userId ,userId == $user.id, $tid : transactionID , $sid : sequentialID, $ecdt : callDateTime) from entry-point BankEvent
	BankEvent(userId == $userId,
				 this != $e,transactionID == $tid,
				 sequentialID < $sid,
				 callDateTime.year  == $ecdt.year,			     
				 callDateTime.month  == $ecdt.month,
			     callDateTime.day  == $ecdt.day,
			     callDateTime.hours  == $ecdt.hours,
			     callDateTime.minutes  == $ecdt.minutes,
			     callDateTime.seconds > $ecdt.seconds
			     ) from entry-point BankEvent
then
	#Rechazar transaccion.
	actuator.writeOutput(kcontext, hashMap.get($userId));
end


# 5th Fraud Pattern
rule "Identical transactions"
dialect "mvel" 
when
	$firstEvent : BankEvent() from entry-point BankEvent
	
	BankEvent(this after[0, 30s] $firstEvent,
				 userId == $firstEvent.userId,
				 amount == $firstEvent.amount) from entry-point BankEvent
then
	#Rechazar transaccion.
	actuator.writeOutput(kcontext, hashMap.get($firstEvent.userId));
end

/*
# 6th Fraud Pattern
rule "Exact coincidence of 2 events at Start Time Stamp , same User"
dialect "mvel" 
when
	$user : User()
	exists ( $e : GenericEvent(userId == $user.id , $ecdt : callDateTime) from entry-point GenericEventEntryPoint and
	  		GenericEvent(userId == $user.id, this != $e, 
				    callDateTime.day == $ecdt.day,
				    callDateTime.month == $ecdt.month,
				    callDateTime.year == $ecdt.year,
				    callDateTime.hours == $ecdt.hours,
				    callDateTime.minutes == $ecdt.minutes,
				    callDateTime.seconds == $ecdt.seconds) from entry-point GenericEventEntryPoint)
then
	actuator.writeOutput(kcontext, hashMap.get($user.id));
	$user.increaseFraudProbability(10);	
end
*/
# 7th Fraud Pattern
rule "Too many events in a short period of time"
dialect "mvel" 

when
	$user : User()
	Number($count : intValue, intValue > 3) from accumulate($event : BankEvent(userId == $user.id) over window:time(1m) from entry-point BankEvent , count($event)) 
then
	$user.increaseFraudProbability(20);
	actuator.writeOutput(kcontext, $user);	 
end
/*
#Support Rule for 8th Fraud Pattern
rule "At Least 3 Events From an User"
dialect "mvel"
salience -200000
no-loop
when
	 $user : User()  
	 Number(intValue == 3) from accumulate( $e : GenericEvent( userId == $user.id) from entry-point GenericEventEntryPoint,count($e))
	 $users : UsersList()	 
then
	System.out.println("Fraud Pattern (Rule 8) can be fired " + $user.id );
	modify($users){
		add($user);			
	}
	System.out.println("Number of Users:" + $users.size());
end


# 8th Fraud Pattern
rule "Wide Distance Between Events"
dialect "mvel"
salience -200001
when
	 exists UsersList()
	 UsersList($users:list)
	 $user : User() from $users
	 $e1 : GenericEvent( userId == $user.id, $tid : transactionID)  from entry-point GenericEventEntryPoint and
	  $e2 : GenericEvent( this after [1h] $e1 ,userId == $user.id, transactionID == $tid)  from entry-point GenericEventEntryPoint and
	  GenericEvent( this after [1h] $e2 ,userId == $user.id, transactionID == $tid)  from entry-point GenericEventEntryPoint
then
	actuator.writeOutput(kcontext, hashMap.get($user.id));
	$user.increaseFraudProbability(10);
	System.out.println(" Users :" + $users.size);
end

*/

# 9th Fraud Pattern
rule "Excesive amount"
dialect "mvel" 
when
	BankEvent($userId : userId, amount > 10000) from entry-point BankEvent 
then
	hashMap.get($userId).increaseFraudProbability(30);
	actuator.writeOutput(kcontext, hashMap.get($userId));
end

rule "High fraud probability"
salience 1000

when
	$user : User(fraudProbability > 90)
	$event : BankEvent(userId == $user.id) from entry-point BankEvent 
then
	mailActuator.writeOutput(kcontext, $user);
	retract($event);
end
